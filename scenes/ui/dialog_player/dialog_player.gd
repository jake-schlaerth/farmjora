extends CanvasLayer

@export_file ("*json") var scene_text_file: String

var scene_text := {}
var selected_text := []
var in_progress := false

@onready var text_label := $Background/TextLabel
@onready var background := $Background

func _ready() -> void:
	background.visible = false
	scene_text = load_scene_text()
	signal_bus.display_dialog.connect(on_display_dialog)

func load_scene_text() -> Dictionary:
	var file := FileAccess.open(scene_text_file, FileAccess.READ)
	return JSON.parse_string(file.get_as_text())

func show_text() -> void:
	text_label.text = selected_text.pop_front()
	
func next_line() -> void:
	if selected_text.size() > 0:
		show_text()
	else:
		finish()

func finish() -> void:
	text_label.text = ""
	background.visible = false
	in_progress = false
	get_tree().paused = false

func on_display_dialog(text_key: String) -> void:
	if in_progress:
		next_line()
	else:
		get_tree().paused = true
		background.visible = true
		in_progress = true
		selected_text = scene_text[text_key].duplicate()
		show_text()
